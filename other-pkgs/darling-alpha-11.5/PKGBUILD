# Maintainer: Ariel Abreu <facekapow@outlook.com>
# Contributor: James Brink <brink.james@gmail.com>
# Contributor: X0rg

_gitname=darling
pkgbase=$_gitname-alpha-git
pkgname='darling-alpha-git'
pkgver=0.1.20230310
pkgrel=1
pkgdesc="Darwin/macOS emulation layer for Linux"
arch=('x86_64')
url="https://www.darlinghq.org"
license=('GPL3')
groups=('darling-git')
depends=('xz' 'fuse' 'libxml2' 'icu' 'openssl' 'bzip2' 'zlib' 'libsystemd'
         'wget' 'curl' 'sqlite' 'ruby' 'sed' 'libarchive' 'file' 'python' 'gawk' 'libunwind' 'ffmpeg'
         'libpng' 'cairo' 'libtiff' 'glu' 'libbsd' 'libxrandr' 'libxkbcommon' 'lib32-gcc-libs' 'libxkbfile')
_depends_x86_64=('lib32-clang' 'lib32-bzip2' 'lib32-systemd' 'lib32-libxslt')
makedepends=('git' 'cmake' 'clang' 'bison' 'flex' 'binutils>=2.28' 'libpng' 'cairo' 'libtiff' 'glu' 'libbsd' 'python2' 'ffmpeg' 'git-lfs' 'llvm' 'vulkan-headers'
             'libxrandr' 'libxkbcommon' 'libxkbfile')
_make_depends_x86_64=('gcc-multilib' 'lib32-gcc-libs')
conflicts=('darling' 'darling-git' 'darling-bin')
provides=('darling' 'darling-git' 'darling-bin')

# Darling git repo and all submodules.
source=('git+https://github.com/darlinghq/darling.git#branch=update_sources_11.5_alpharelease2')

# We skip md5 on all git repos
md5sums=( 'SKIP' )
options=('!buildflags')

pkgver() {
    cd "$srcdir/$_gitname"

    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_gitname"

    echo "Initialize git submodules."
    git submodule update --init --recursive
#
#     echo "Updating LFS files"
#     cd "$srcdir/$_gitname/src/external/swift"
#     git lfs pull

    echo "Creating build directory."
    cd "$srcdir/$_gitname"
    mkdir -pv "build"
}

build() {
    cd "$srcdir/$_gitname/build"

    echo "Running cmake."
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr

    echo "Running make."
    make
}

package() {
    cd "$srcdir/$_gitname/build"
    make DESTDIR="$pkgdir" install
}
